plugins {
   id("com.android.application")
   id("dagger.hilt.android.plugin")
   id("toml-version-bump")
   kotlin("android")
   kotlin("kapt")
}

configure<si.inova.kotlinova.gradle.versionbump.TomlVersionBumpExtension> {
   // Search for all instances of versions.json, generated by the gradle versions plugin.
   versionReportFiles.set(
      fileTree(rootDir).apply {
         include("**/build/dependencyUpdates/versions.json")
      }
   )

   // Location of the toml file that we should bump versions for
   tomlFile.set(File(rootDir, "libs.toml"))
}

android {
   compileSdk = 33

   namespace = "com.matejdro.runpeewear"

   defaultConfig {
      applicationId = "com.matejdro.runpeewear"
      minSdk = 26
      targetSdk = 31

      versionCode = 1
      versionName = "1.0"
   }

   compileOptions {
      sourceCompatibility(JavaVersion.VERSION_11)
      targetCompatibility(JavaVersion.VERSION_11)
   }

   buildFeatures {
      compose = true
   }

   composeOptions {
      kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
   }
}

kotlin {
   jvmToolchain(11)
}

dependencies {
   implementation(project(":common"))

   implementation(libs.androidx.activity)
   implementation(libs.androidx.activity.compose)
   implementation(libs.androidx.lifecycle.viewmodel)
   implementation(libs.androidx.compose.foundation)
   implementation(libs.androidx.compose.material)
   implementation(libs.androidx.compose.ui)
   implementation(libs.dagger.hilt.runtime)
   implementation(libs.kotlin.coroutines.playServices)
   implementation(libs.logcat)
   implementation(libs.libsu)
   implementation(libs.libsu.io)
   implementation(libs.playServices.wearable)
   kapt(libs.dagger.hilt.compiler)
}
